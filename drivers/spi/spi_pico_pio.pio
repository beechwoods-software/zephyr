; Copyright (c) 2023 Stephen Boylan <stephen.boylan@beechwoods.com>
; SPDX-License-Identifier: Apache-2.0

; RP2040 PIO programs for SPI
; Delays are used to keep clock symmetric
; Each version uses 4 clock cycles

; CPOL=0, CPHA=0
.program spi_mode_0_0
.side_set 1

    out pins, 1  side 0  [1]
    in  pins, 1  side 1  [1]

; CPOL=1, CPHA=1
.program spi_mode_1_1
.side_set 1

    out x, 1     side 1
    mov pins, x  side 0 [1]
    in  pins, 1  side 1

; This 3-wire code runs at 4 clock cycles per bit
; 3-wire, CPOL=0, CPHA=0
; Output stage
.program spi_sio_mode_0_0_tx
.side_set 1

.wrap_target
    pull                side 0
wrloop:
    out pins, 1         side 0  [1]
    jmp !osre wrloop    side 1  [1]
.wrap

; Input stage
; Number of **WORDS** to read should be set in the X register
; Number of bits ih a WORD must be put into the FIFO each time
; (Yes, that's annoying, but there's only the two scratch registers
; and no way to branch on the input shift register value.)
.program spi_sio_mode_0_0_rx
.side_set 1

.wrap_target
wordloop:
    pull                side 0
    out y, 32           side 0
bitloop:
    nop                 side 1  [1]
    in pins, 1          side 0
    jmp y--, bitloop    side 0
    push                side 0
    jmp x--, wordloop   side 0
.wrap
